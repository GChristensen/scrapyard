plugins {
    id 'com.android.application'
    id 'kotlin-android'
    //id 'com.github.sgtsilvio.gradle.android-retrofix'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "l2.albitron.scrapyard"
        minSdk 21
        targetSdk 33
        versionCode 20
        versionName "2.2.2"

        String dropboxKey = localProperties['DBX_API_KEY']
        buildConfigField "String", "DBX_API_KEY", "\"${dropboxKey}\""
        manifestPlaceholders = [dropboxKey: dropboxKey, msalSigHash: ""]

        //testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders.msalSigHash = localProperties['MSAL_SIG_HASH']
        }
        debug {
            manifestPlaceholders.msalSigHash = localProperties['MSAL_SIG_HASH_DEBUG']
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/AL2.0', 'META-INF/LGPL2.1',
                         'META-INF/LICENSE.md', 'META-INF/NOTICE.md',
                         'META-INF/io.netty.versions.properties']
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        //dataBinding true
    }
}

dependencies {

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.5'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.5'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.5'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.jsoup:jsoup:1.14.3'

    //implementation 'net.sourceforge.streamsupport:android-retrofuture:1.7.4'

    implementation 'com.dropbox.core:dropbox-core-sdk:5.0.0'

    implementation ("com.microsoft.identity.client:msal:2.2.2") { exclude group: 'com.microsoft.device.display' }
    implementation ('com.microsoft.graph:microsoft-graph:3.1.0') {
        exclude group: 'javax.activation'
        exclude group: 'xpp3'
    }

    //testImplementation 'junit:junit:4.+'
    //androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

def getLocalProperties() {
    Properties props = new Properties()
    if (file('../local.properties').exists()) {
        props.load(new FileInputStream(file('../local.properties')))
    }
    return props
}
